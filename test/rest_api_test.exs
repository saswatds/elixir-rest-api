defmodule RestApiTest.Router do
  import Plug.BasicAuth
  use ExUnit.Case
  use Plug.Test

  @opts RestApi.Router.init([])

  test "GET / should return ok" do
    conn = conn(:get, "/")
    conn = put_req_header(conn, "authorization", encode_basic_auth("user", "secret"))

    conn = RestApi.Router.call(conn, @opts)

    assert conn.status == 200
    assert conn.resp_body == "OK"
  end

  describe "Posts" do
    # The setup callback is called before each test and the on_exit after each of
    # the test is complete
    # We will use this hook to list all the mongo db collections and for each of
    # the collection to clear out the entire collection. This way for every test
    # case we will start from a clean slate
    setup do
      on_exit fn ->
        Mongo.show_collections(:mongo)
        |> Enum.each(fn col -> Mongo.delete_many!(:mongo, col, %{}) end)
      end
    end

    test "POST /post should create a post" do
      # Assert that there are no elements in the db
      assert Mongo.find(:mongo, "Posts", %{}) |> Enum.count == 0

      # Make an API to call create a post
      conn = conn(:post, "/post", %{name: "Post 1", content: "Content of post"})
      conn = put_req_header(conn, "authorization", encode_basic_auth("user", "secret"))
      conn = RestApi.Router.call(conn, @opts)

      # Checking that response code was 200
      assert conn.status == 200

      # Asserting that response body was what we expected
      # Note: We are using pattern matching here to perform the assertion
      # The id is autogenerated by mongodb so we have not way of predicting it
      # therefore we just use _ to match to anything but expect it to exist
      assert %{
        "id" => _,
        "content" => "Content of post",
        "name" => "Post 1"
      } = Jason.decode!(conn.resp_body)

      # Assert that there is something in the db
      # Task: It is very naive to check that the post was created. We should ideally
      # also check if the content was correct. Go ahead and try it out and post your
      # answer in the comment section below.
      assert Mongo.find(:mongo, "Posts", %{}) |> Enum.count == 1
    end

    def createPosts() do
      result = Mongo.insert_many!(:mongo, "Posts",[
        %{name: "Post 1", content: "Content 1"},
        %{name: "Post 2", content: "Content 2"},
      ])

      result.inserted_ids |> Enum.map(fn id -> BSON.ObjectId.encode!(id) end)
    end

    test "GET /posts should fetch all the posts" do
      createPosts()

      conn = conn(:get, "/posts")
      conn = put_req_header(conn, "authorization", encode_basic_auth("user", "secret"))
      conn = RestApi.Router.call(conn, @opts)

      assert conn.status == 200

      resp = Jason.decode!(conn.resp_body);

      assert Enum.count(resp) == 2

      assert %{
        "id" => _,
        "content" => "Content 1",
        "name" => "Post 1"
      } = Enum.at(resp, 0)

      assert %{
        "id" => _,
        "content" => "Content 2",
        "name" => "Post 2"
      } = Enum.at(resp, 1)

    end

    test "GET /post/:id should fetch a single post" do
      [id | _] = createPosts()

      conn = conn(:get, "/post/#{id}")
      conn = put_req_header(conn, "authorization", encode_basic_auth("user", "secret"))
      conn = RestApi.Router.call(conn, @opts)

      assert conn.status == 200

      assert %{
        "id" => _,
        "content" => "Content 1",
        "name" => "Post 1"
      } = Jason.decode!(conn.resp_body)
    end

    test "PUT /post/:id should update a post" do
      [id | _] = createPosts()

      conn = conn(:put, "/post/#{id}", %{content: "Content 3"})
      conn = put_req_header(conn, "authorization", encode_basic_auth("user", "secret"))
      conn = RestApi.Router.call(conn, @opts)

      assert conn.status == 200

      assert %{
        "id" => _,
        "content" => "Content 3",
        "name" => "Post 1"
      } = Jason.decode!(conn.resp_body)
    end

    test "DELETE /post/:id should delete a post" do
      [id | _] = createPosts()

      assert Mongo.find(:mongo, "Posts", %{}) |> Enum.count == 2

      conn = conn(:delete, "/post/#{id}", %{content: "Content 3"})
      conn = put_req_header(conn, "authorization", encode_basic_auth("user", "secret"))
      conn = RestApi.Router.call(conn, @opts)

      assert conn.status == 200

      assert Mongo.find(:mongo, "Posts", %{}) |> Enum.count == 1
    end
  end
end
